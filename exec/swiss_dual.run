# To execute: include "code".run

reset;
model "../models/SVM_dual.mod";
data "../data/swiss_train.dat";
option solver cplex;
# cplex, gurobi, minos

solve;

# Retrieving w from lambda
for {j in 1..N} {
    let w[j] := sum {i in 1..M} la[i] * y[i] * X[i, j];
}
	
display w;

# Retrieving gamma
param gamma_tmp;
param count_tmp;

let gamma_tmp := 0;
let count_tmp := 0;

for {i in 1..M} {
    if (la[i] > 1e-6 && la[i] < 0.99*nu) then {
        let gamma_tmp := gamma_tmp + (y[i] - sum {j in 1..N} w[j] * X[i,j]);
        let count_tmp := count_tmp + 1;
    }
}

let gamma := gamma_tmp / count_tmp;
display gamma;
display count_tmp;


# Accuracy computation
reset data M, N, nu, X, y;

data "../data/swiss_test.dat";

param pred{i in 1..M}, default 0;


let {i in 1..M} pred[i] :=
    if sum{j in 1..N} w[j] * X[i,j] + gamma >= 0 then 1 else -1;


param correct{i in 1..M} :=
    if pred[i] = y[i] then 1 else 0;

param total_correct := sum{i in 1..M} correct[i];
param accuracy := total_correct / M;

display total_correct, accuracy;
